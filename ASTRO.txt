00010 C -------PROGRAM FOR ASTRO CLOCK-------
00020 C CALCULATES SUNRISE AND SUNSET, AUTOMATICALLY ADJUSTS FOR DAY LIGHT SAVINGS TIME
00030 C
00040 C THIS SOFTWARE IS OWNED AND MAINTAINED BY SIEMENS BUILDING TECHNOLOGIES, INC.
00050 C ALL RIGHTS RESERVED. ANY MODIFICATIONS WITHOUT THE EXPRESS, WRITTEN
00060 C CONSENT FROM SIEMENS BUILDING TECHNOLOGIES, INC. MAY VOID THE WARRANTY.
00070 C
00080 C VER DATE ENG/SPEC COMMENTS
00090 C A XX-XXXX H.NEWMAN INITIAL PROGRAM
00100 C B 11-2001 A.MANZO XXX
00110 C C 03-2001 DEHRING XXX
00120 C D 03-2009 G.ACKERSON DST CHANGES
00140 C
00150 C
00160 C VIRTUAL ANALOG OUTPUTS
00170 C
00180 C \NAME FOR THE CABINET OA VIRTUAL IS ...................(VLAO)\ASTRO.SUNRISE\
00190 C \NAME FOR THE HEATING VALVE VIRTUAL OUTPUT.............(VLAO)\ASTRO.SUNSET\
00200 C \NAME FOR THE MIXING DAMPERS VIRTUAL OUTPUT............(VLAO)\SUNRISE\
00210 C \NAME FOR THE SUPPLY AIR CONTROLLER IS ................(VLAO)\SUNSET\
00220 C
00230 C VIRTUAL DIGITAL OUTPUTS
00240 C
00250 C \NAME FOR THE LIGHTING TRIGGER ........................(VLDO)\ASTRO.LTG\
00260 C
00270 C LOCAL POINTS
00280 C
00290 C \DAY OF YEAR ..........................................(LOCAL)\$LOC1\
00300 C \PLACE HOLDER FOR CALCULATION .........................(LOCAL)\$LOC2\
00310 C \PLACE HOLDER FOR CALCULATION .........................(LOCAL)\$LOC3\
00320 C \PLACE HOLDER FOR CALCULATION .........................(LOCAL)\$LOC4\
00330 C \PLACE HOLDER FOR CALCULATION .........................(LOCAL)\$LOC5\
00340 C \PLACE HOLDER FOR CALCULATION .........................(LOCAL)\$LOC6\
00350 C \PLACE HOLDER FOR CALCULATION .........................(LOCAL)\$LOC7\
00360 C \PLACE HOLDER FOR CALCULATION .........................(LOCAL)\$LOC8\
00370 C \END OF SEGMENT .......................................(LOCAL)\$LOC15\
00380 C \DAY LIGHT SAVINGS TIME ...............................(LOCAL)\DST\
00390 C \DAY ..................................................(LOCAL)\D\
00400 C \DAY OF MONTH .........................................(LOCAL)\DM\
00410 C
00420 C .... BAY AREA LATITUDE AND LONGITUDES .... 
00430 C SAN FRANCISCO LAT - 37.37 LONG - 122.23
00440 C SAN MATEO LAT - 33.38 LONG - 117.58
00450 C SAN JOSE LAT - 37.37 LONG - 121.92
00460 C OAKLAND LAT - 37.73 LONG - 122.22
00470 C HAYWARD LAT - 37.65 LONG - 122.12
00480 C
00490 C -- THE LATITUDE IS ENTERED IN LINE 470 --
00500 C -- THE LONGITUDE IS ENTERED IN LINE'S 590& 600 --
00510 C
00520 C DEFINE (X,)
00530 C
00540 C .... USER DEFINED LOCAL POINTS .... 
00560 C LOCAL (DST,D,DM)
00570 C
00580 C .... CALCULATE THE DAY OF YEAR .... 
00590 C
00600 IF(MONTH .EQ. 1) THEN $LOC1 = DAYOFM
00610 IF(MONTH .EQ. 2) THEN $LOC1 = DAYOFM + 31
00620 IF(MONTH .EQ. 3) THEN $LOC1 = DAYOFM + 59
00630 IF(MONTH .EQ. 4) THEN $LOC1 = DAYOFM + 90
00640 IF(MONTH .EQ. 5) THEN $LOC1 = DAYOFM + 120
00650 IF(MONTH .EQ. 6) THEN $LOC1 = DAYOFM + 151
00660 IF(MONTH .EQ. 7) THEN $LOC1 = DAYOFM + 181
00670 IF(MONTH .EQ. 8) THEN $LOC1 = DAYOFM + 212
00680 IF(MONTH .EQ. 9) THEN $LOC1 = DAYOFM + 243
00690 IF(MONTH .EQ. 10) THEN $LOC1 = DAYOFM + 273
00700 IF(MONTH .EQ. 11) THEN $LOC1 = DAYOFM + 304
00710 IF(MONTH .EQ. 12) THEN $LOC1 = DAYOFM + 334
00720 C
00730 C .... ASTRONOMICAL CALCULATIONS ****
00740 C
00750 $LOC2 = 23.45 * SIN(360 * (284 + $LOC1) / 365)
00760 C V---- LATITUDE
00770 $LOC3 = TAN(37.37) * TAN($LOC2)
00780 $LOC4 = (-(90) - ATN($LOC3 / SQRT(1 - $LOC3 * $LOC3))) / 15
00790 $LOC5 = 12 + $LOC4
00800 $LOC6 = 12 - $LOC4
00810 $LOC7 = 360 * ($LOC1 - 81) / 364
00820 $LOC8 = 9.87 * SIN(2 * $LOC7) - 7.53 * COS($LOC7) - 1.5 * SIN($LOC7)
00830 C
00840 $LOC15 = $LOC15
00850 C
00860 C .... ADJUST FOR DAYLIGHT SAVINGS TIME ****
00870 C
00880 LOCAL (DST, D, DM)
00890 OFF ($DST)
00900 $D = DAY
00910 IF (MONTH .EQ. 3 .AND. DAY .EQ. 7) THEN $D = 0
00920 $DM = DAYOFM - $D
00930 IF (MONTH .EQ. 3 .AND. $DM .GT. 7) THEN ON ($DST)
00940 IF (MONTH .GT. 3 .AND. MONTH .LT. 11) THEN ON ($DST)
00950 IF (MONTH .EQ. 11 .AND. $DM .LT. 1) THEN ON ($DST)
00960 C
00970 $LOC15 = $LOC15
00980 C
00990 C .... CALCULATE SUNRISE AND SUNSET TIME ****
01000 C                                             V------LONGITUDE
01010 "ASTRO.SUNRISE" = ($LOC5 - $LOC8 / 60 - 4 * (120 - 122.23) / 60) + $DST
01020 C                                                    V------LONGITUDE
01030 "ASTRO.SUNSET" = ($LOC6 - $LOC8 / 60 - 4 * (120 - 122.23) / 60) + $DST
01040 C
01050 $LOC15 = $LOC15
01060 C
01070 C .... DAILY COMMAND OF SUNRISE/SUNSET TRIGGER ****
01080 C
01090 IF(TIME .GT. "ASTRO.SUNRISE" .AND. TIME .LT. "ASTRO.SUNSET") THEN OFF("ASTRO.LTG") ELSE ON("ASTRO.LTG")
01100 C
01110 C
01470 C .... END OF PROGRAM CONTROL .... 
01480 C
01490 GOTO 520